#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Windows.Script.txt"			as Windows
#Include "Libs/miltant/ObstacleV2/ManialinkLib.Script.txt"						as Styles
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/SubMenu/Index.Script.txt"		as SubMenu
#Include "Libs/miltant/ObstacleV2/Titlepack/Dialogs/Explorer/Index.Script.txt"	as ExplorerDialog

declare Boolean G_PendingStartMapEditor;
declare Boolean G_PendingStartGhostsEditor;
declare Boolean G_PendingStartReplayEditor;

Text GetName() {
	return "editors";
}

Void StartMapEditor() {
	if (G_PendingStartMapEditor)
		G_PendingStartMapEditor = !ExplorerDialog::StartMapEditor();
}

Void StartGhostsEditor() {
	if (G_PendingStartGhostsEditor)
		G_PendingStartGhostsEditor = !ExplorerDialog::StartGhostsEditor();
}

Void StartReplayEditor() {
	if (G_PendingStartReplayEditor)
		G_PendingStartReplayEditor = !ExplorerDialog::StartReplayEditor();
}

Void Load() {
	G_PendingStartMapEditor = False;
	G_PendingStartGhostsEditor = False;
	G_PendingStartReplayEditor = False;

	declare SubMenu::K_SubMenu[] SubMenus;

	declare SubMenu::K_SubMenu Maps;
	Maps.Name = "Maps";
	Maps.PreventDefault = True;
	Maps.Image = "maps";
	SubMenus.add(Maps);

	declare SubMenu::K_SubMenu Replay;
	Replay.Name = "Replay";
	Replay.PreventDefault = True;
	Replay.Image = "replay";
	SubMenus.add(Replay);

	declare SubMenu::K_SubMenu Item;
	Item.Name = "Item";
	Item.PreventDefault = True;
	Item.Image = "item";
	SubMenus.add(Item);

	declare SubMenu::K_SubMenu Action;
	Action.Name = "Action";
	Action.PreventDefault = True;
	Action.Image = "action";
	SubMenus.add(Action);

	declare SubMenu::K_SubMenu Interface;
	Interface.Name = "Interface";
	Interface.PreventDefault = True;
	Interface.Image = "interface";
	SubMenus.add(Interface);

	declare SubMenu::K_SubMenu Ghost;
	Ghost.Name = "Ghost";
	Ghost.PreventDefault = True;
	Ghost.Image = "ghost";
	SubMenus.add(Ghost);


	declare Windows::K_Window Window;
	Window.Name = GetName();
	Window.Manialink = SubMenu::Build(SubMenus);
	Window.Animation = CUILayer::EUILayerAnimation::LeftSlow;

	Windows::CreateWindow(Window);
}

Void Loop() {
	foreach(Event in PendingEvents) {
		if ( Event.Type == CManiaAppEvent::EType::LayerCustomEvent &&
			 Event.CustomEventData.count > 0 &&
			 Event.CustomEventType == "window_load") {
			switch (Event.CustomEventData[0]) {
				case "Maps": {
					ExplorerDialog::Load("Map", True);
					G_PendingStartMapEditor = True;
				}
				case "Replay": {
					ExplorerDialog::Load("Replay");
					G_PendingStartReplayEditor = True;
				}
				case "Item": {
					wait(TitleControl.IsReady);
					TitleControl.OpenEditor(CTitleControl::EEditorType::ItemEditor);
				}
				case "Action": {
					wait(TitleControl.IsReady);
					TitleControl.OpenEditor(CTitleControl::EEditorType::ActionMaker);
				}
				case "Interface": {
					wait(TitleControl.IsReady);
					TitleControl.OpenEditor(CTitleControl::EEditorType::InterfaceDesigner);
				}
				case "Ghost": {
					ExplorerDialog::Load("Map");
					G_PendingStartGhostsEditor = True;
				}
				case "Module": {
					wait(TitleControl.IsReady);
					TitleControl.OpenEditor(CTitleControl::EEditorType::ModuleStudio);
				}
			}
		}
	}

	StartMapEditor();
	StartReplayEditor();
	StartGhostsEditor();
}