#Include "Libs/miltant/Nadeo_Fork/Common/Core/MenuLayers.Script.txt"				as Layers

#Include "Libs/miltant/ObstacleV2/ManialinkLib.Script.txt"						    as Styles
#Include "Libs/ahmad3/EventAPI/Types.Script.txt"								    as EventAPITypes
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Windows.Script.txt"			    as Windows
#Include "Libs/miltant/ObstacleV2/Titlepack/Menus/Play/Campaign/Index.Script.txt"   as Campaign

Text BuildSideBar() {
	return """
{{{Styles::GetStyleSheet()}}}
<frame id="Frame_CampaignSideBar">
	<framemodel id="CampaignEditionItem">
		<label id="Label_CampaignEdition" halign="center" textprefix="$t" class="lato" textcolor="" textsize="4.8" scriptevents="1"/>
	</framemodel>

	<frame>
		<frame pos="31 -24">
			<frameinstance modelid="CampaignEditionItem" id="CampaignEditionItem_1"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -7.2" id="CampaignEditionItem_2"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -14.4" id="CampaignEditionItem_3"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -21.6" id="CampaignEditionItem_4"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -28.8" id="CampaignEditionItem_5"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -36" id="CampaignEditionItem_6"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -43.2" id="CampaignEditionItem_7"/>
			<frameinstance modelid="CampaignEditionItem" pos="0 -50.4" id="CampaignEditionItem_8"/>
		</frame>

		<quad pos="6.5 -120" id="Quad_Refresh" valign="center" halign="right" size="5 5" style="Icons64x64_1" substyle="Refresh" scriptevents="1" />
		<label pos="12 -120" textsize="1.3" text="Refresh" halign="center" valign="center" class="lato"/>
		<quad pos="10.67 -5" size="7.67 7.67"  image="file://Media/Images/Obstacle/Campaign/MX.png" valign="center"/>
		<label pos="34 -5.5" textsize="5.2" text="seasons" textprefix="$t" halign="center" valign="center2" class="lato"/>
		<quad size="62 123.83" image="file://Media/Images/Obstacle/Campaign/Aside.png"/>
	</frame>
</frame>
<script><![CDATA[

#Include "MathLib" as ML
#Include "TextLib" as TL
#Include "ColorLib" as CL

{{{EventAPITypes::ML_Preproc()}}}

Void InitCampaignEditionsList() {
	declare K_Event P_CampaignEvent for Page;
	declare Integer P_SelectedEdition for Page;

	declare Integer ControlIndex = 0;
	foreach (I => Edition in P_CampaignEvent.Editions) {
		ControlIndex += 1;

		declare CMlFrame Frame_CampaignEdition = (Page.GetFirstChild("CampaignEditionItem_" ^ ControlIndex) as CMlFrame);
		declare CMlLabel Label_CampaignEdition = (Frame_CampaignEdition.GetFirstChild("Label_CampaignEdition") as CMlLabel);

		declare EditionName = Edition.name;
		if (Edition.subtitle != "")
			EditionName ^= " " ^ Edition.subtitle;

		Label_CampaignEdition.SetText(EditionName);
		Label_CampaignEdition.DataAttributeSet("editionid", TL::ToText(I));

		declare Vec3 TextColor = CL::HexToRgb("B4B4B4");
		if (P_SelectedEdition == I) {
			TextColor = CL::HexToRgb("FFFFFF");
		}

		Label_CampaignEdition.TextColor = TextColor;
	}
}

main() {
	declare Text P_SelectedEventHandle for Page = "";
	declare Integer P_SelectedEdition for Page = -1;
	
	declare Text SelectedCampaignHandle = "";
	declare Integer SelectedCampaign = -1;

	wait(P_SelectedEdition != -1);

	while (True) {
		if (SelectedCampaign != P_SelectedEdition || SelectedCampaignHandle != P_SelectedEventHandle) {
			InitCampaignEditionsList();
		}

		yield;
	}
}

]]></script>
""";
}

Void Load() {
	Campaign::SetEventHandle("campaign");
	Campaign::Load([Windows::K_ManialinkLayer {
		Name = "L_CampaignSideBar",
		Manialink = BuildSideBar()
	}]);
}

Void Loop() {
	Campaign::Loop(Windows::GetPage("L_CampaignSideBar"));
}