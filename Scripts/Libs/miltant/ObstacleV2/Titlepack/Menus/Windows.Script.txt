#Include "Libs/miltant/Nadeo_Fork/Common/Core/MenuLayers.Script.txt"	as Layers


#Struct K_Window {
	Text Name;
	Text Manialink;
	CUILayer::EUILayerAnimation Animation;
	Text[Text] SubLayers;
}

declare K_Window[Text] G_Windows;
declare Text G_Current_Window;
declare K_Window G_CurrentRealWindow;


Text Build(K_Window _Menu) {
	declare ML_Text = """
<manialink version="3" name="L_Window_{{{_Menu.Name}}}">
{{{_Menu.Manialink}}}
</manialink>
	""";

	return ML_Text;
}

Text Current() {
	return G_Current_Window;
}

Void CreateWindow(K_Window _Window) {
	if (!G_Windows.existskey(_Window.Name)) {
		G_Windows[_Window.Name] = _Window;
		declare LayerName = "L_Window_" ^ _Window.Name;
		declare Text MLText = Build(_Window);

		Layers::Create(LayerName, MLText);
		Layers::Get(LayerName).AttachId = _Window.Name;

		foreach (Name => Manialink in _Window.SubLayers) {
			declare SubLayerName = LayerName ^ "_SubLayer_" ^ Name;
			Layers::Create(SubLayerName, Manialink);
			Layers::Get(SubLayerName).AttachId = Name;
			Layers::SetAnimationTypeIn(SubLayerName, _Window.Animation);
		}

		Layers::SetAnimationTypeIn(LayerName, _Window.Animation);
	}
}

K_Window GetWindow(Text _Name) {
	if (G_Windows.existskey(_Name))
		return G_Windows[_Name];
	return K_Window{};
}

CMlPage GetPageOfSubLayer(K_Window _Window, Text _SubLayerName) {
	if (!_Window.SubLayers.existskey(_SubLayerName)) {
		return Null;
	}

	declare CUILayer Layer <=> Layers::Get("L_Window_" ^ _Window.Name ^ "_SubLayer_" ^ _SubLayerName);
	if (Layer == Null) {
		return Null;
	}

	return Layer.LocalPage;
}

CMlPage GetPageOfSubLayer(Text _WindowName, Text _SubLayerName) {
	if (!G_Windows.existskey(_WindowName)) {
		return Null;
	}

	return GetPageOfSubLayer(GetWindow(_WindowName), _SubLayerName);
}

CUILayer GetLayer(Text _Name) {
	declare LayerName = "L_Window_" ^ _Name;
	return Layers::Get(LayerName);
}

CMlPage GetPage(Text _Name) {
	declare Layer = GetLayer(_Name);
	if (Layer != Null)
		return Layer.LocalPage;
	
	return Null;
}

Void Unload() {
	Layers::Detach(G_Current_Window);
	foreach (Name => _Unused in G_CurrentRealWindow.SubLayers) {
		Layers::Detach(G_Current_Window ^ "_SubLayer_" ^ Name);
	}
	G_Current_Window = "";
}

Void Load(Text _Window) {
	Unload();

	if (G_Windows.existskey(_Window)) {
		declare LayerName = "L_Window_" ^ _Window;

		Layers::Attach(LayerName);

		G_Current_Window = LayerName;
		G_CurrentRealWindow = G_Windows[_Window];

		foreach (Name => _Unused in G_Windows[_Window].SubLayers) {
			Layers::Attach(LayerName ^ "_SubLayer_" ^ Name);
		}

		if (Layers::Exists("Obstacle_Background")) {
			switch (_Window) {
				case "home": {
					LayerCustomEvent(Layers::Get("Obstacle_Background"), "Titlepack_Background_Show", []);
				}
				default: {
					LayerCustomEvent(Layers::Get("Obstacle_Background"), "Titlepack_Background_Hide", []);
				}
			}
		}
	}
}


Void Loop() {
	foreach(Event in PendingEvents) {
		if (Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
			switch (Event.CustomEventType) {
				case "window_load": {
					// asserts that the window's name is provided
					// and that preventdefault is not set
					if (
						Event.CustomEventData.count > 0
					 && Event.CustomEventData.count == 1
					 || Event.CustomEventData[1] != "preventdefault"
					)
					{ Load(Event.CustomEventData[0]); }
				}
				case "window_unload" : {
					Unload();
				}
			}
		}
	}
}