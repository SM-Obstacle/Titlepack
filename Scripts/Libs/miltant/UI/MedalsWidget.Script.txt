#Include "TextLib" as TL

#Include "Libs/miltant/Nadeo_Fork/CustomUI.Script.txt" as CustomUI
#Include "Libs/smokegun/Constants.Script.txt" as Constants
#Include "Libs/smokegun/Player.Script.txt" as Player

#Include "Libs/ahmad3/UI/CachedRequests.Script.txt" as CachReqs
#Include "Libs/ahmad3/ErrorReport.Script.txt" as ErrReport
#Include "Libs/ahmad3/ErrMgr.Script.txt" as ErrMgr
#Include "Libs/ahmad3/Ban.Script.txt" as Ban
#Include "Libs/ahmad3/Events.Script.txt" as ObsEvents
#Include "Libs/ahmad3/RequestManager.Script.Txt" as ReqMgr

/*********************************************
    CONSTANTS
*********************************************/

#Const  Version     "2023-28-06"
#Const  ScriptName  "RecordsWidget.Script.txt"

#Const C_Ev_TimeInLb "medalswidget:timeinlb"

Void Init() {
    if (ObsEvents::HasFailed() || !ObsEvents::HasEventLoaded()) return;

    declare netwrite Integer[][Text] Net_MedalsTimesPerMap for Teams[0] = [];
    declare netwrite Integer Net_MedalsTimesPerMapUpdated for Teams[0] = -1;

    Net_MedalsTimesPerMap.clear();

    foreach (Category in ObsEvents::CurrentEdition().categories) {
        foreach (Map in Category.maps) {
            Net_MedalsTimesPerMap[Map.map_uid] = [Map.bronze_time, Map.silver_time, Map.gold_time, Map.champion_time];
        }
    }

    Net_MedalsTimesPerMapUpdated = Now;
}

Void Yield() {
    foreach (Event in UIManager.PendingEvents) {
        if (
            Event.Type != CUIConfigEvent::EType::OnLayerCustomEvent
            || Event.CustomEventType != C_Ev_TimeInLb
        ) {
            continue;
        }

        declare Integer TimeInLb = TL::ToInteger(Event.CustomEventData[0]);

        declare CSmPlayer Player <=> GetPlayer(Event.UI);
        Player::setTimeInLb(Player, TimeInLb);
    }
}

Text Private_GetSmallRecords() {
    CustomUI::Add("Frame_Medals_Records", <139., 75.>, <44., 34.>);
    return """
<frame id="Frame_Medals_Records" class="LibCustomUI_Module">
    <framemodel id="small_record">
        <label id="rank" pos="-20 0" size="6 4" halign="left" valign="center2" style="TextCardSmallScores2" textsize="1" textfont="light"/>
        <label id="name" pos="-14 0" size="20 4" halign="left" valign="center2" textsize="1" textfont="light"/>
        <label id="time" pos="20 0" size="14 4" halign="right" valign="center2" style="TextTitle2" textsize="1" textfont="OswaldMono"/>
    </framemodel>

    <frame pos="139 75">
        <frame id="Frame_Records" pos="0 -3">
            <frameinstance modelid="small_record" pos="0 0" id="record0"/>
            <frameinstance modelid="small_record" pos="0 -4" id="record1"/>
            <frameinstance modelid="small_record" pos="0 -8" id="record2"/>

			<frame pos="-20 -13" id="Frame_Medal_3">
				<quad pos="-1" size="4 4" image="file://Media/Images/Obstacle/Medals/champion.png" valign="center"/>
        		<label pos="6 0" size="20 4" halign="left" valign="center2" textsize="1" text="Master Time" textfont="light"/>
        		<label id="time" pos="40" size="14 4" halign="right" valign="center2" style="TextTitle2" textsize="1" textfont="OswaldMono"/>
			</frame>

			<frame pos="-20 -18" id="Frame_Medal_2">
				<quad pos="-1" size="4 4" image="file://Media/Images/Obstacle/Medals/gold.png" valign="center"/>
        		<label pos="6 0" size="20 4" halign="left" valign="center2" textsize="1" text="Gold Time" textfont="light"/>
        		<label id="time" pos="40" size="14 4" halign="right" valign="center2" style="TextTitle2" textsize="1" textfont="OswaldMono"/>
			</frame>

			<frame pos="-20 -23" id="Frame_Medal_1">
				<quad pos="-1" size="4 4" image="file://Media/Images/Obstacle/Medals/silver.png" valign="center"/>
        		<label pos="6 0" size="20 4" halign="left" valign="center2" textsize="1" text="Silver Time" textfont="light"/>
        		<label id="time" pos="40" size="14 4" halign="right" valign="center2" style="TextTitle2" textsize="1" textfont="OswaldMono"/>
			</frame>

			<frame pos="-20 -28" id="Frame_Medal_0">
				<quad pos="-1" size="4 4" image="file://Media/Images/Obstacle/Medals/bronze.png" valign="center"/>
        		<label pos="6 0" size="20 4" halign="left" valign="center2" textsize="1" text="Bronze Time" textfont="light"/>
        		<label id="time" pos="40" size="14 4" halign="right" valign="center2" style="TextTitle2" textsize="1" textfont="OswaldMono"/>
			</frame>
        </frame>

    	<quad size="44 34" style="BgRaceScore2" substyle="BgCardPlayer" halign="center" valign="top" scriptevents="1"/>
    </frame>
</frame>
    """;
}

Text GetLayer() {
    return """
<manialink version="3" name="Obstacle:RecordsWidget">
<frame>
    {{{Private_GetSmallRecords()}}}
</frame>
<script><!--
    #Include "TextLib" as TL

    #Struct K_RankedRecord {
        Integer rank;
        Text login;
        Text nickname;
        Integer time;
    }

    #Struct K_Response {
        K_RankedRecord[] response;
    }

    {{{ReqMgr::ML_Preproc("ReqMgr")}}}
    {{{Ban::ML_Preproc("BanMgr")}}}
    {{{ErrMgr::ML_Preproc("ErrMgr", "BanMgr")}}}

    {{{ReqMgr::ML_Content("ReqMgr")}}}
    {{{Ban::ML_Content("BanMgr")}}}
    {{{ErrMgr::ML_Content("ErrMgr", "TL", "BanMgr")}}}
    {{{ErrReport::ML_Content("ErrReport")}}}

    declare Integer[][Text] G_MedalsTimesPerMap;

    declare ReqMgr_K_GetReqId G_ReqId;
    declare ReqMgr_K_GetRequest G_Req;

    declare CSmPlayer G_LocalPlayer;
    declare Integer[Text] G_CampaignRecords;
    declare Integer G_CurrentPlayerRank;

    declare CMlFrame G_Frame_Widget;
    declare CMlFrame G_Frame_Records;

    Integer GetMedal(Integer Medal) {
        if (G_MedalsTimesPerMap.existskey(Map.MapInfo.MapUid)) {
            return G_MedalsTimesPerMap[Map.MapInfo.MapUid][Medal];
        }
        switch (Medal) {
            case 0: {
                return Map.MapInfo.TMObjective_BronzeTime;
            }
            case 1: {
                return Map.MapInfo.TMObjective_SilverTime;
            }
            case 2: {
                return Map.MapInfo.TMObjective_GoldTime;
            }
        }
        return Map.MapInfo.TMObjective_AuthorTime;
    }

    Void UpdateUI(K_Response _Response) {
        declare PlayerRank = -1;
        declare Integer LocalPlayerTime = -1;
        foreach (I => Record in _Response.response) {
            if (Record.login == G_LocalPlayer.User.Login) {
                G_CampaignRecords[Map.MapInfo.MapUid] = Record.time;
                G_CurrentPlayerRank = I + 1;
                PlayerRank = I;

                LocalPlayerTime = Record.time;

                break;
            }
        }

        if (LocalPlayerTime != -1 && G_LocalPlayer == InputPlayer) {
            SendCustomEvent({{{dump(C_Ev_TimeInLb)}}}, [LocalPlayerTime ^ ""]);
        }

        if (PlayerRank == -1) {
            G_CampaignRecords[Map.MapInfo.MapUid] = 0;
            G_CurrentPlayerRank = _Response.response.count + 2;
        }

        // medals to display
        declare Integer[] Medals = [GetMedal(0), GetMedal(1), GetMedal(2), GetMedal(3)];
        // non-null ranks to display
        declare Integer[] Ranks;
        if (_Response.response.count > 0) {
            Ranks.add(0);

            if (PlayerRank < 3 || PlayerRank == -1) {
                if (_Response.response.count > 1)
                    Ranks.add(1);
                if (_Response.response.count > 2)
                    Ranks.add(2);
            } else {
                Ranks.add(PlayerRank - 1);
                Ranks.add(PlayerRank);
            }
        }

        declare CurrentHeight = 0.;
        declare CurrentMedal = Medals.count - 1;
        declare CurrentTime = Medals[0] + 1;

        foreach (I => Rank in Ranks) {
            CurrentTime = _Response.response[Rank].time;
            
            // displays medals before the current rank
            while (CurrentMedal >= 0 && Medals[CurrentMedal] < CurrentTime) {
                declare Frame <=> (G_Frame_Records.GetFirstChild("Frame_Medal_" ^ CurrentMedal) as CMlFrame);
                declare timeLabel <=> (Frame.GetFirstChild("time") as CMlLabel);

                Frame.RelativePosition_V3.Y = CurrentHeight;
                timeLabel.SetText(TL::TimeToText(Medals[CurrentMedal], True));
                CurrentHeight -= 5.;
                CurrentMedal -= 1;
            }

            // displays the current rank
            declare frame <=> (G_Frame_Records.GetFirstChild("record" ^ I) as CMlFrame);
            declare rankLabel <=> (frame.GetFirstChild("rank") as CMlLabel);
            declare nameLabel <=> (frame.GetFirstChild("name") as CMlLabel);
            declare timeLabel <=> (frame.GetFirstChild("time") as CMlLabel);

            frame.RelativePosition_V3.Y = CurrentHeight;
            CurrentHeight -= 4.;
            if (I < Ranks.count) {
                rankLabel.SetText(""^_Response.response[Rank].rank);
                nameLabel.SetText(_Response.response[Rank].nickname);
                timeLabel.SetText(TL::TimeToText(_Response.response[Rank].time, True));
            }
        }

        // displays medals after the last rank
        while (CurrentMedal >= 0) {
            declare Frame <=> (G_Frame_Records.GetFirstChild("Frame_Medal_" ^ CurrentMedal) as CMlFrame);
            declare timeLabel <=> (Frame.GetFirstChild("time") as CMlLabel);

            Frame.RelativePosition_V3.Y = CurrentHeight;
            timeLabel.SetText(TL::TimeToText(Medals[CurrentMedal], True));
            CurrentHeight -= 5.;
            CurrentMedal -= 1;
        }

        // hides unused ranks
        for (I, Ranks.count, 2) {
            declare frame <=> (G_Frame_Records.GetFirstChild("record" ^ I) as CMlFrame);
            declare rankLabel <=> (frame.GetFirstChild("rank") as CMlLabel);
            declare nameLabel <=> (frame.GetFirstChild("name") as CMlLabel);
            declare timeLabel <=> (frame.GetFirstChild("time") as CMlLabel);

            frame.RelativePosition_V3.Y = CurrentHeight;
            CurrentHeight -= 4.;
            
            rankLabel.SetText("");
            nameLabel.SetText("---");
            timeLabel.SetText("--:--:--");
        }

    }

    Void UpdateRecords(Text _Login) {
        if (ReqMgr_IsPending(G_ReqId) && !ReqMgr_IsWaiting(G_ReqId)) {
            Http.Destroy(ReqMgr_GetReq(G_ReqId));
        }

        declare Text Params = "?mapId="^Map.Id^"&playerId=" ^ _Login;
        declare Text Route = {{{dump(ObsEvents::PrefixRoute("overview"))}}} ^ Params;
        G_Req = ReqMgr_K_GetRequest {
            Route = Route,
            UseCache = False,
            Headers = ReqMgr_DefaultHeaders()
        };

        G_ReqId = ReqMgr_Make(G_Req);
    }

    Void LoopUpdateRecords() {
        if (!ReqMgr_IsPending(G_ReqId)) return;

        if (ReqMgr_IsWaiting(G_ReqId)) {
            G_ReqId = ReqMgr_MakePending(G_ReqId);
            if (ReqMgr_IsWaiting(G_ReqId)) return;
        }

        declare CHttpRequest Req <=> ReqMgr_GetReq(G_ReqId);
        if (!Req.IsCompleted) return;
        G_ReqId = ReqMgr_NullGetReq();

        if (Req.StatusCode == 200) {
            declare K_Response Res;
            Res.fromjson(Req.Result);
            UpdateUI(Res);
        } else {
            declare ErrMgr_K_Err Err = ErrMgr_Handle(Req.StatusCode, Req.Result);
            if (Err.Err.type != ErrMgr_C_Err_Maintenance) {
                ErrReport_ReportGet(G_Req.Route, Req.Result);
            }
        }

        Http.Destroy(Req);
    }

    {{{CachReqs::ML_WaitCacheSentFn("HasSentCachedReqs")}}}

    main() {
        {{{CustomUI::InjectMLInit()}}}
        wait(InputPlayer != Null && HasSentCachedReqs());

        G_ReqId = ReqMgr_NullGetReq();

		declare Integer[Text] CampaignRecords for LocalUser;
        G_CampaignRecords = CampaignRecords;

        declare netread Integer[][Text] Net_MedalsTimesPerMap for Teams[0];
        declare netread Integer Net_MedalsTimesPerMapUpdated for Teams[0] = -1;
        declare Integer MedalsTimesPerMapUpdated = -1;

        declare netread Net_StartTime for InputPlayer = -1;
        declare netread Net_CheckpointsTotal for Teams[0] = -1;
        declare netread Net_CpProgress for InputPlayer = -1;
        declare netread Net_Time1 for InputPlayer = 0;
        declare Integer CurrentPlayerRank for LocalUser = 0;
        G_CurrentPlayerRank = CurrentPlayerRank;
        declare netread Net_RecordsUpdated for Teams[0] = -1;
        declare LastUpdate = -1;
        declare Running = False;

        declare LastPlayerLogin = InputPlayer.User.Login;
        declare UpdateUI = False;

        G_Frame_Widget <=> (Page.GetFirstChild("Frame_Medals_Records") as CMlFrame);
        G_Frame_Records <=> (G_Frame_Widget.GetFirstChild("Frame_Records") as CMlFrame);

        while (True) {
            yield;

            G_LocalPlayer <=> InputPlayer;
            if (GUIPlayer != Null) G_LocalPlayer <=> GUIPlayer;
            if (G_LocalPlayer == Null) continue;

            if (G_LocalPlayer.User.Login != LastPlayerLogin) {
                LastUpdate = -1;
                LastPlayerLogin = G_LocalPlayer.User.Login;
            }

            if (Net_MedalsTimesPerMapUpdated != MedalsTimesPerMapUpdated) {
                G_MedalsTimesPerMap = Net_MedalsTimesPerMap;
                MedalsTimesPerMapUpdated = Net_MedalsTimesPerMapUpdated;
            }

            if (Net_CpProgress < 0 || Net_CheckpointsTotal < 0 || Net_StartTime < 0) {
                if (G_Frame_Widget.Visible)
                    G_Frame_Widget.Hide();
            } else if (!G_Frame_Widget.Visible) {
                G_Frame_Widget.Show();
            }

            if (Net_RecordsUpdated != LastUpdate) {
                log("Records updated " ^ LastUpdate ^ " -> " ^ Net_RecordsUpdated ^ " (running: " ^ Running ^ ")");
                LastUpdate = Net_RecordsUpdated;
                UpdateRecords(G_LocalPlayer.User.Login);
                Running = False;
            }

            if (Net_CpProgress > -1) {
                Running = True;
            }

            LoopUpdateRecords();

            foreach (Event in PendingEvents) {
               switch (Event.Type) {
                    case CMlScriptEvent::Type::MouseClick: {
                        OpenLink({{{dump(Constants::C_WebsiteUrl ^ "/" ^ ObsEvents::PrefixRoute("map/"))}}} ^ Map.Id,  CMlScript::LinkType::ExternalBrowser);
                    }
                }
            }
            {{{CustomUI::InjectMLLoop()}}}
        }
    }
--></script>
</manialink>
    """;
}
